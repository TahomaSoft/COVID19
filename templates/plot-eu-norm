set terminal pngcairo dashed font "Helvetica,12" size 800,600
set output "../graphs/covid-eu-norm.png"
set logscale y
set key at 0,100 box
set label "Updates at http://nrg.cs.ucl.ac.uk/mjh/covid19" at graph 0.02,0.98 font "Helvetica,11" textcolor "#a0a0a0"
set yrange [10:YMAX]
set xrange [-30:2]
set ylabel "COVID19 cases per million inhabitants"
set xlabel "Days (from DATE March 2020)"
offset = OFFSET
set mxtics
set grid mxtics xtics ytics mytics
choff = 0.0
esoff = 1
froff = 12
deoff = 12.5
nloff = 12.5
ukoff = 18
plot "../country_data/italy" using ($0 +5 - offset):($2/itpop) w l lw 2 t "Italy", \
"../country_data/spain" using ($0 + 10 + esoff  - offset):($2/espop) w l lw 2 lc "orange" t sprintf("Spain (%d day ahead of Italy)", esoff), \
"../country_data/switzerland" using ($0 +10 - choff-offset):($2/chpop) w l lw 2 lc "brown" t sprintf("Switzerland (%d days behind Italy)", choff), \
"../country_data/france" using ($0 + 10 - froff - offset):($2/frpop) w l lw 2 lc "red" t sprintf("France (%d days behind Italy)", froff), \
"../country_data/germany" using ($0 + 9 - deoff - offset):($2/depop) w l lw 2 lc "#00b000" t sprintf("Germany (%.1f days behind Italy)", deoff), \
"../country_data/netherlands" using ($0 + 12 - nloff -offset):($2/nlpop) w l lw 2 lc "#a0a0a0" t sprintf("Netherlands (%.1f days behind Italy)", nloff), \
"../country_data/uk" using ($0 +12 - ukoff - offset):($2/ukpop) w l lw 2 lc "blue" t sprintf("UK (%.1f days behind Italy)", ukoff), \
1.22**(x+offset + 2) lc "black" dashtype 2 t "22% daily increase",\
1.135**(x+offset + 18.5) lc "black" dashtype 5 t "13.5% daily increase",\
1.08**(x+offset + 53.3) lc "black" dashtype 3 t "8% daily increase"


#"../country_data/france" using ($0 + 10 - froff - offset):($2/frpop) w l lw 2 lc "red" t sprintf("France (%d days behind Italy)", froff), \
#"../country_data/germany" using ($0 + 9 - deoff - offset):($2/depop) w l lw 2 lc "#00b000" t sprintf("Germany (%d days behind Italy)", deoff), \
#"../country_data/netherlands" using ($0 + 12 - nloff -offset):($2/nlpop) w l lw 2 lc "#a0a0a0" t sprintf("Netherlands (%d days behind Italy)", nloff), \
#"../country_data/uk" using ($0 +12 - ukoff - offset):($2/ukpop) w l lw 2 lc "blue" t sprintf("UK (%d days behind Italy)", ukoff), \
