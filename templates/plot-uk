set terminal pngcairo dashed font "Helvetica,12" size 800,600
set output "../graphs/covid-uk.png"
set logscale y
offset = OFFSET
set key at -1,50 box
set yrange [5:4000]
set xrange [5 - OFFSET:]
set ylabel "COVID19 cases per million inhabitants"
set xlabel "Days (from DATE April 2020)"
set mxtics
set grid mxtics xtics ytics mytics
ukpop = 67.7
frpop = 65.2
depop = 83.7
itpop = 60.5
supop = 8.6
espop = 46.7
nopop = 5.4
lompop = 10
atpop = 9.0
sepop = 9.1
lonpop = 8.9
nwpop = 7.2
epop = 6.2
swpop = 5.6
nepop = 8.1
midpop = 10.7

lonoff = 9.5
seoff = 18
midoff = 16
eoff = 19
swoff = 19
neoff = 19
nwoff = 17.5

set grid
plot "../aligned/country_data/italy" using ($1):($2/itpop) w l lc "#a0a0a0" lw 2 t "All of Italy", \
"../aligned/uk/uk-regions-cum" using ($1 - lonoff):($3/lonpop) w l lw 2 lc "red" t sprintf("London, UK (%d days behind Italy)", lonoff), \
"../aligned/uk/uk-regions-cum" using ($1 - midoff):($4/midpop) w l lw 2 lc "#00a000" t sprintf("Midlands, UK (%d days behind Italy)", midoff), \
"../aligned/uk/uk-regions-cum" using ($1 - seoff):($7/sepop) w l lw 2 lc "#00a0ff" t sprintf("South East, UK (%d days behind Italy)", seoff), \
"../aligned/uk/uk-regions-cum" using ($1 - neoff):($5/nepop) w l lw 2 lc "blue" t sprintf("North East, UK (%d days behind Italy)", neoff), \
"../aligned/uk/uk-regions-cum" using ($1 - nwoff):($6/nwpop) w l lw 2 lc "brown" t sprintf("North West (%d days behind Italy)", nwoff), \
"../aligned/uk/uk-regions-cum" using ($1 - eoff):($2/epop) w l lw 2 lc "orange" t sprintf("UK, East (%d days behind Italy)", eoff), \
"../aligned/uk/uk-regions-cum" using ($1 - swoff):($8/swpop) w l lw 2 lc "magenta" t sprintf("South West, UK, (%d days behind Italy)", swoff),\
1.35**(x+offset - 4) lc "black"  dt 4 t "35% daily increase",\
1.22**(x+offset + 2) lc "black" dashtype 2 t "22% daily increase", \
1.16**(x+offset + 9) lc "black" dashtype 3 t "16% daily increase"

#"switzerland" using ($0+3.5-offset):($2/supop) w lp ps 0.5 lw 2 lc "brown" t "Switzerland (11 days behind Lombardy, see comment)", \
#"austria" using ($0 +4.5):($2/atpop) w lp ps 0.5 lw 2 lc "orange" t "Austria (14 days behind Lombardy)", \

